plugins {
    id 'java'
    id 'maven-publish'
}

group = 'org.mangorage'

repositories {
    mavenCentral()
    maven {
        url = "https://maven.minecraftforge.net/"
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    compileOnly('com.google.code.gson:gson:2.11.0')
    runtimeOnly('com.google.code.gson:gson:2.11.0')

    compileOnly("net.minecraftforge:json-data-utils:0.2.3")
}

test {
    useJUnitPlatform()
}

tasks.register('generateResource') {
    def outputDir = "$buildDir/generated-resources"
    outputs.dir outputDir

    doLast {
        def file = file("$outputDir/internal/buildinfo.properties")
        file.parentFile.mkdirs()
        file.text = """
            version=${project.version}
            buildTime=${new Date().format("yyyy-MM-dd'T'HH:mm:ss")}
        """.stripIndent()
    }
}

processResources {
    from("$buildDir/generated-resources")
    dependsOn(tasks.named('generateResource'))
}

publishing {
    publications {
        register("mangomodloader-api", MavenPublication) {
            artifact jar // uses the API subproject's jar + sources/javadoc if configured

            pom {
                name = 'MangoModLoader API'
                description = 'API module for MangoModLoader'
                url = 'https://github.com/MangoModLoader/MangoModLoader'
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MangoModLoader/MangoModLoader/issues'
                }
                developers {
                    developer {
                        id = 'mangorage'
                        name = 'MangoRage'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.mangorage.org/releases"
            credentials {
                username = "mango"
                password = "$MAVEN_PASSWORD"
            }
        }
    }
}
