import  org.mangorage.mmlg.util.GitVersion;

plugins {
    id 'java'
    id "org.mangorage.mangomodloadergradle" version "1.3.0"
    id 'net.minecraftforge.gradle' version '7.0.0-beta.33'
    id "net.minecraftforge.jarjar" version "0.1.0"
    id 'maven-publish'
}

group = 'org.mangorage'
version = GitVersion.getGitVersion().getVersionAsString() + "$TAG_APPEND"

System.out.println(version)

repositories {
    mavenCentral()
    maven {
        url = "https://maven.minecraftforge.net/"
    }
    maven {
        url = "https://libraries.minecraft.net/"
    }
    maven {
        url = uri("https://maven.terraformersmc.com/")
    }

    maven minecraft.maven
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
}

jarJar.register() {
    it.archiveClassifier = ""
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: "net.minecraftforge.jarjar"

    version = GitVersion.getGitVersion().getVersionAsString() + "$TAG_APPEND"
    println "${subproject.name} -> ${version}"

    // Only create generateResource in specific subprojects
    if (subproject.name in ['installer', 'mangomodloader-api']) {
        tasks.register('generateResource') {
            def outputDir = "$buildDir/generated-resources"
            outputs.dir outputDir

            doLast {
                def file = file("$outputDir/META-INF/mangomodloader/buildinfo.properties")
                file.parentFile.mkdirs()
                file.text = """
                    version=${version}
                    mc_version=${MC_VERSION}
                    buildTime=${new Date().format("yyyy-MM-dd'T'HH:mm:ss")}
                """.stripIndent()
            }
        }

        subproject.tasks.named('processResources') {
            from("$buildDir/generated-resources")
            dependsOn(tasks.named('generateResource'))
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    tasks.withType(JavaCompile) {
        options.release.set(21)
    }
}

minecraft {
    mappings channel: 'official', version: "${MC_VERSION}"
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    bootJar(project(":"))
    gameJar(minecraft.dep("net.minecraft:joined:${MC_VERSION}")) {
        attributes {
            attribute(Attribute.of("net.minecraftforge.native.operatingSystem", String), "windows")
        }
    }

    jarJar('com.mojang:jtracy:1.0.29') {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    // Mixin

    jarJar("net.fabricmc:sponge-mixin:0.15.4+mixin.0.8.7") {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    jarJar("io.github.llamalad7:mixinextras-common:0.4.1") {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    jarJar("org.ow2.asm:asm:9.8") {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    jarJar("org.ow2.asm:asm-util:9.8") {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    jarJar("org.ow2.asm:asm-analysis:9.8") {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    jarJar("org.ow2.asm:asm-tree:9.8") {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    jarJar("org.ow2.asm:asm-commons:9.8") {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    // Forge

    jarJar('net.minecraftforge:modlauncher:10.2.4') {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    jarJar('net.minecraftforge:bootstrap-api:2.0.0') {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    jarJar('net.minecraftforge:unsafe:0.9.2') {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }

    jarJar('net.minecraftforge:securemodules:2.2.21') {
        jarJar.configure(it) {
            module {
                group = 'libraries'
            }
        }
    }



    // Normal

    jarJar(project(":mixinsupport")) {
        jarJar.configure(it) {
            module {
                group = 'game'
            }
            version = '1.1.0'
        }
    }

    jarJar(project(":mangomodloader-api")) {
        jarJar.configure(it) {
            module {
                group = 'loader'
            }
            version = '2.0.0'
        }
    }

    java {
        modularity.inferModulePath = true
    }
}

sourceSets {
    main {
        // Make resources go into the same output folder as classes
        output.resourcesDir = output.classesDirs.singleFile
    }
}


test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.mangorage.boot.Boot',
                'Automatic-Module-Name': 'org.mangorage.mangomodloader.boot'
        )
    }
}

publishing {
    publications {
        register("mangomodloader", MavenPublication) {
            artifact tasks.named("jarJar").get()  // <- this replaces 'jar'
            // artifact sourcesJar
            // artifact javadocJar

            pom {
                name = 'MangoModLoader'
                description = 'MangoModLoader'
                url = 'https://github.com/MangoModLoader/MangoModLoader'
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MangoModLoader/MangoModLoader/issues'
                }
                developers {
                    developer {
                        id = 'mangorage'
                        name = 'MangoRage'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.mangorage.org/releases"
            credentials {
                username = "mango"
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}


tasks.getByName("jarJar").dependsOn("mixinsupport:build")