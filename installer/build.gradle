plugins {
    id 'java'
    id 'maven-publish'
}

group = 'org.mangorage'

jarJar.register() {
    it.archiveClassifier = ""
}

repositories {
    mavenCentral()
    maven {
        url = "https://maven.mangorage.org/releases"
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    jarJar(project(":"))
    jarJar('net.minecraftforge:minecraft-mavenizer:0.3.19')
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.mangorage.mml.installer.Installer'
        )
    }
}

// Task to run the jar after building
task runInstaller(type: Exec) {
    dependsOn tasks.getByName("jarJar") // make sure the jar is built first
    group "installer"

    // working directory
    def runDir = file("build/run")
    setWorkingDir(runDir)

    // ensure folder exists before running
    doFirst {
        runDir.mkdirs()
    }

    commandLine 'java', '-jar', jar.archiveFile.get().asFile.absolutePath
}

// Make sure installer:jarJar depends on the main jar tasks
tasks.named("jarJar") {
    dependsOn(":jar")       // ensure the regular jar runs first
    dependsOn(":jarJar")    // if it's supposed to depend on another jarJar task
}

publishing {
    publications {
        register("mangomodloader", MavenPublication) {
            artifact tasks.named("jarJar").get()  // <- this replaces 'jar'
            // artifact sourcesJar
            // artifact javadocJar

            pom {
                name = 'MangoModLoader'
                description = 'MangoModLoader'
                url = 'https://github.com/MangoModLoader/MangoModLoader'
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MangoModLoader/MangoModLoader/issues'
                }
                developers {
                    developer {
                        id = 'mangorage'
                        name = 'MangoRage'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.mangorage.org/releases"
            credentials {
                username = "mango"
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}